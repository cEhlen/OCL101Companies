model one_o_one_companies

-----------------------------------------------------------
-- Classes

class Company
attributes
    Name : String
operations
    total() : Real =
        self.departments->collect(d : Department | d.total())->sum()
    cut()
    complyRankedSalary() : Boolean =
        self.departments->forAll(d : Department | d.complyRankedSalary())
    containsEmployee(e: Employee) : Boolean =
        self.departments->forAll(d : Department | d.containsEmployee(e))
    containsDepartment(dep: Department) : Boolean =
        self.departments->includes(dep) or
        self.departments->one(d : Department | d <> dep and d.containsDepartment(dep))

    addDepartment(d : Department)
    removeDepartment(d : Department)
end

class Department
attributes
    Name : String
operations
    total() : Real =
        self.employees->collect(Salary)->sum()
    cut()
    complyRankedSalary() : Boolean =
        self.employees->sortedBy(e : Employee | e.Salary)->last().oclIsTypeOf(Manager) and
        self.subdepartments->forAll(d : Department | d.complyRankedSalaryRec(self.employees->sortedBy(e : Employee | e.Salary)->first().Salary))

    complyRankedSalaryRec(max : Real) : Boolean =
        self.employees->sortedBy(e : Employee | e.Salary)->last().oclIsTypeOf(Manager) and
        self.employees->sortedBy(e : Employee | e.Salary)->last().Salary < max and
        self.subdepartments->forAll(d : Department | d.complyRankedSalaryRec(self.employees->sortedBy(e : Employee | e.Salary)->first().Salary))

    containsEmployee(e : Employee) : Boolean =
        self.employees->includes(e) or self.subdepartments->one(d : Department | d.containsEmployee(e))

    containsDepartment(dep : Department) : Boolean =
        self.subdepartments->includes(dep) or self.subdepartments->one(d : Department | d.containsDepartment(dep))

    getCompany() : Company =
        if self.company.oclIsUndefined() then
            self.parent.getCompany()
        else
            self.company
        endif

    addEmployee(e : Employee)
    removeEmployee(e : Employee)
    addDepartment(d : Department)
    removeDepartment(d : Department)
end

class Employee
attributes
    Name : String
    Salary : Real
    Address : String
    Employer : Company
operations
    cut()
end

class Manager < Employee
end

-----------------------------------------------------------
-- Associations

association WorksIn between
    Employee[*] role employees
    Department[1] role department
end

association SubdepartmentOf between
    Department[*] role subdepartments
    Department[0..1] role parent
end

association DepartmentOf between
    Department[*] role departments
    Company[1] role company
end

-----------------------------------------------------------
-- Constraints
constraints


-------------
-- Company
-------------
context Company
    inv needs_a_name: self.Name.size() > 0
    inv comply_ranked_salary: self.complyRankedSalary()

context Company::addDepartment(d : Department)
    pre department_not_in_company: not self.containsDepartment(d)
    pre department_has_no_company: d.getCompany().oclIsUndefined()
    post department_has_company: d.getCompany() = self
    post department_was_added: self.containsDepartment(d)

context Company::removeDepartment(d : Department)
    pre department_in_company: self.containsDepartment(d)
    post department_was_removed: not self.containsDepartment(d)
    post employees_fired: self.total() + d.total() = (self@pre).total()

context Company::cut()
    post salary_was_cut: self.total() = (self@pre).total() / 2.0

-------------
-- Department
-------------
context Department
    inv needs_a_name: self.Name.size() > 0
    inv comply_ranked_salary: self.getCompany().complyRankedSalary()
    inv needs_a_manager: employees->one(e : Employee | e.oclIsTypeOf(Manager))

context Department::cut()
    post salary_was_cut: self.total() = (self@pre).total() / 2.0

context Department::addEmployee(e : Employee)
    pre employee_not_in_company: not self.getCompany().containsEmployee(e)
    pre employee_not_employed_by_company: e.Employer <> self.getCompany()
    post employee_employed_by_company: e.Employer = self.getCompany()
    post employee_works_for_department: employees->includes(e)

context Department::removeEmployee(e : Employee)
    pre employee_works_in_department: employees->includes(e)
    post employee_works_no_longer_for_company: not self.getCompany().containsEmployee(e)

context Department::addDepartment(d : Department)
    pre department_not_self: d <> self
    pre department_not_already_in_company: not self.getCompany().containsDepartment(d)
    pre department_has_no_company: d.getCompany().oclIsUndefined()
    post department_was_added: self.subdepartments->includes(d)

context Department::removeDepartment(d : Department)
    pre department_is_subdepartment: self.subdepartments->includes(d)
    post department_was_removed: not self.getCompany().containsDepartment(d)

-------------
-- Employee
-------------
context Employee
    inv salary_must_be_greater_than_zero: Salary > 0
    inv needs_a_name: Name.size() > 0
    inv needs_an_address: Address.size() > 0

context Employee::cut()
    post salary_was_cut: Salary = 0.5 * Salary@pre

